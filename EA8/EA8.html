<!DOCTYPE html5>
<html>
	<head>
		<script src="ext/gl-matrix.js"></script>
		<script src="EA8_vertexData_plane.js"></script>
		<script src="EA8_vertexData_sphere.js"></script>
		<script src="EA8_vertexData_torus.js"></script>
		<script src="EA8.js"></script>
		<script type="x-shader/x-vertex" id="vertexshader">
			attribute vec3 aPosition;
			attribute vec3 aNormal;
			 
			uniform mat4 uPMatrix;
			uniform mat4 uMVMatrix;
			uniform mat3 uNMatrix;
			 
			varying vec3 vNormal;
			varying vec4 vPosition;
			 
			void main(){
				// Calculate vertex position in eye coordinates. 
				vec4 tPosition = uMVMatrix * vec4(aPosition, 1.0);
				// Calculate projektion.
				gl_Position = uPMatrix * tPosition;
			 
				vec3 tNormal = normalize(uNMatrix * aNormal);
			 
				vPosition = tPosition;
				vNormal = tNormal;
			}
		</script>
		<script type="x-shader/x-fragment" id="fragmentshadertoon">
			precision mediump float;
 
			varying vec3 vNormal;
			varying vec4 vPosition;
			 
			// Material.
			struct PhongMaterial {
				vec3 ka;
				vec3 kd;
				vec3 ks;
				float ke; 
			};
			uniform PhongMaterial material;
			 
			// Ambient light.
			uniform vec3 ambientLight;
			 
			// Pointlights.
			const int MAX_LIGHT_SOURCES = 8;
			struct LightSource {
				bool isOn;
				vec3 position;
				vec3 color;
			};
			uniform LightSource light[MAX_LIGHT_SOURCES];
			 
			// Phong illumination for single light source,
			// no ambient light.
			vec3 phong(vec3 p, vec3 n, vec3 v, LightSource l) {
			 
				vec3 L = l.color;
			 
				vec3 s = normalize(l.position - p);
				vec3 r = reflect(-s, n);
			 
				float sn = max( dot(s,n), 0.0);
				float rv = max( dot(r,v), 0.0);

				if (max(dot(v, n), 0.) < 0.3) {
					return vec3(0.);
				} else {
					if (sn == 0.) {
						sn += 0.1337;
					}
					if (sn < .9) {
						sn = ceil(sn / 0.25) * 0.25;
					} else {
						sn = 1.;
					}
					if (rv < .98) {
						rv = 0.;
					}
				}
			 
				vec3 diffuse = material.kd * L * sn;
			 
				vec3 specular = material.ks * L * pow(rv, material.ke);
			 
				return diffuse + specular;			
			}
			 
			// Phong illumination for multiple light sources
			vec3 phong(vec3 p, vec3 n, vec3 v) {
			 
				// Calculate ambient light.
				vec3 result = material.ka * ambientLight;
			 
				// Add light from all light sources.
				for(int j=0; j < MAX_LIGHT_SOURCES; j++){
					if(light[j].isOn){
						result += phong(p, n, v, light[j]);
					}
				}
				return result;
			}
			 
			void main() {
				// Calculate view vector.
				vec3 v = normalize(-vPosition.xyz);
			 
				vec3 vNormal = normalize(vNormal);
			 
				gl_FragColor = vec4( phong(vPosition.xyz, vNormal, v), 1.0);
			}
		</script>
		<script type="x-shader/x-fragment" id="fragmentshader">
			precision mediump float;
 
			varying vec3 vNormal;
			varying vec4 vPosition;
			 
			// Material.
			struct PhongMaterial {
				vec3 ka;
				vec3 kd;
				vec3 ks;
				float ke; 
			};
			uniform PhongMaterial material;
			 
			// Ambient light.
			uniform vec3 ambientLight;
			 
			// Pointlights.
			const int MAX_LIGHT_SOURCES = 8;
			struct LightSource {
				bool isOn;
				vec3 position;
				vec3 color;
			};
			uniform LightSource light[MAX_LIGHT_SOURCES];
			 
			// Phong illumination for single light source,
			// no ambient light.
			vec3 phong(vec3 p, vec3 n, vec3 v, LightSource l) {
			 
				vec3 L = l.color;
			 
				vec3 s = normalize(l.position - p);
				vec3 r = reflect(-s, n);
			 
				float sn = max( dot(s,n), 0.0);
				float rv = max( dot(r,v), 0.0);
			 
				vec3 diffuse = material.kd * L * sn;
			 
				vec3 specular = material.ks * L * pow(rv, material.ke);
			 
				return diffuse + specular;			
			}
			 
			// Phong illumination for multiple light sources
			vec3 phong(vec3 p, vec3 n, vec3 v) {
			 
				// Calculate ambient light.
				vec3 result = material.ka * ambientLight;
			 
				// Add light from all light sources.
				for(int j=0; j < MAX_LIGHT_SOURCES; j++){
					if(light[j].isOn){
						result += phong(p, n, v, light[j]);
					}
				}
				return result;
			}
			 
			void main() {
				// Calculate view vector.
				vec3 v = normalize(-vPosition.xyz);
			 
				vec3 vNormal = normalize(vNormal);
			 
				gl_FragColor = vec4( phong(vPosition.xyz, vNormal, v), 1.0);
			}
		</script>
		<script>
			function changeStyle(style) {
				document.getElementById('style').textContent = style;
				app.start();	
			}
		</script>
	</head>
	<body onload="app.start()">
		<canvas id="canvas" width="500" height="500"></canvas><br>
		Use "I" or "L" for light movement. <br>
		Current Style: <p id=style>Normal</p>
		<span style="display: inline-block; text-align: left;"><button onclick="changeStyle('Normal')">Normal</button></span>
		<span style="display: inline-block; text-align: left;"><button onclick="changeStyle('Toon')">Toon</button></span>
	</body>
</html>